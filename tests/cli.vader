Include: setup.vader

#------------------------------------------------------------------# Parse due #

Execute (ParseDue (date)):
  let scenarios = [
    \ [':15',   1526335200, '15/05/18'],
    \ [':10',   1528581600, '10/06/18'],
    \ [':20',   1526767200, '20/06/18'],
    \ [':153',  1552604400, '15/03/19'],
    \ [':102',  1549753200, '10/02/19'],
    \ [':201',  1547938800, '20/01/19'],
    \ [':158',  1534284000, '15/08/18'],
    \ [':108',  1533852000, '10/08/18'],
    \ [':2010', 1539986400, '20/10/18'],
    \ [':140117',  'throw', '14/01/17'],
  \]

Then (should parse date (date ref: 15/05/18)):
  for [due, expected; comment] in scenarios
    if expected == 'throw'
      AssertThrow kronos#cli#ParseDue(1526335200, due)
    else
      AssertEqual expected, kronos#cli#ParseDue(1526335200, due)
    endif
  endfor

Execute (ParseDue (time)):
  let scenarios = [
    \ ['::15',   1526475600, '16/05/18 15:00'],
    \ ['::10',   1526457600, '16/05/18 10:00'],
    \ ['::20',   1526407200, '15/05/18 20:00'],
    \ ['::1515', 1526390100, '15/05/18 15:15'],
    \ ['::1530', 1526391000, '15/05/18 15:30'],
    \ ['::12',   1526464800, '16/05/18 12:00'],
    \ ['::080',  1526450400, '16/05/18 08:00'],
    \ ['::0130', 1526427000, '16/05/18 01:30'],
    \ ['::16',   1526392800, '15/05/18 16:00'],
    \ ['::2000', 1526407200, '15/05/18 20:00'],
    \ ['::2330', 1526419800, '15/05/18 23:30'],
  \]

Then (should parse time (date ref: 15/05/18 15:15)):
  for [due, expected; comment] in scenarios
    AssertEqual expected, kronos#cli#ParseDue(1526390100, due)
  endfor

Execute (ParseDue (date&time)):
  let scenarios = [
    \ [':155:15',      1526389200, '15/05/18 15:00'],
    \ [':160719:1821', 1563294060, '16/07/19 18:21'],
    \ [':1406:1420',   1528978800, '14/06/18 14:20'],
    \ [':1001:1001',   1547110860, '10/01/19 10:01'],
    \ [':100118:1001',    'throw', '10/01/18 10:01'],
  \]

Then (should parse time (date ref: 15/05/18 15:15)):
  for [due, expected; comment] in scenarios
    if expected == 'throw'
      AssertThrow kronos#cli#ParseDue(1526390100, due)
    else
      AssertEqual expected, kronos#cli#ParseDue(1526390100, due)
    endif
  endfor

#---------------------------------------------------------------------# Create #

Execute (Create):
  call kronos#cli#Create(g_database, 'test')

Then (should create basic task):
  AssertEqual
    \ [{'id': 1, 'desc': 'test', 'tags': [], 'due': v:null}],
    \ kronos#database#ReadTasks(g_database)

Execute:
  call kronos#cli#Create(g_database, '+cli test +a +b-c')

Then (should create task with tags):
  AssertEqual
    \ [{'id': 1, 'desc': 'test', 'tags': ['cli', 'a', 'b-c'], 'due': v:null}],
    \ kronos#database#ReadTasks(g_database)

Execute:
  call kronos#cli#Create(g_database, 'test :0 42')

Then (should create task with due):
  AssertEqual
    \ [{'id': 1, 'desc': 'test 42', 'tags': [], 'due': '0'}],
    \ kronos#database#ReadTasks(g_database)

